default:
    @just --list

install: link-root link-config apps-osx apps-mise apps-npm

update:
    #!/usr/bin/env zsh
    source ~/.shell/zinit
    brew upgrade
    brew cleanup
    rtk init -g --opencode --auto-patch
    zinit self-update
    zinit update
    mise plugins up
    mise up
    nvim --headless -c "lua vim.pack.update(nil, { force = true })" -c "quit"
    mise exec just -- just apps-npm

link-root:
    #!/usr/bin/env bash
    for name in *; do
        case "$name" in
            justfile|README.md|config|.git) continue ;;
            gnupg)
                target="$HOME/.gnupg"
                echo "$target"
                mkdir -p "$target"
                chmod 700 "$target"
                for file in "$PWD/$name"/*; do
                    link_target="$target/$(basename "$file")"
                    rm -f "$link_target"
                    ln -s "$file" "$link_target"
                done
                continue
                ;;
        esac
        target="$HOME/.$name"
        echo "$target"
        rm -rf "$target"
        ln -s "$PWD/$name" "$target"
    done

link-config:
    #!/usr/bin/env bash
    for name in config/*; do
        target="$HOME/.$name"
        echo "$target"
        mkdir -p "$(dirname "$target")"
        rm -rf "$target"
        ln -s "$PWD/$name" "$target"
    done

apps-osx:
    brew update
    brew bundle install --file=Brewfile
    brew cleanup
    rtk init -g --opencode --auto-patch

apps-mise:
    mise plugins install neovim
    mise install

apps-npm:
    npm install -g @fsouza/prettierd
    npm install -g @github/copilot-language-server

check:
    #!/usr/bin/env bash
    echo "Checking dotfiles installation..."
    echo ""
    echo "Binaries:"
    binaries=(nvim zsh git fzf zoxide eza bat fd rg lazygit mise)
    for bin in "${binaries[@]}"; do
        if command -v "$bin" &> /dev/null; then
            echo "  ✓ $bin"
        else
            echo "  ✗ $bin"
        fi
    done

    echo ""
    echo "Git:"
    git config --global user.name &> /dev/null && echo "  ✓ user.name: $(git config --global user.name)" || echo "  ✗ user.name"
    git config --global user.email &> /dev/null && echo "  ✓ user.email: $(git config --global user.email)" || echo "  ✗ user.email"
    echo "  $(test -f "$HOME/.gnupg/gpg-agent.conf" && printf '✓' || printf '✗') gpg-agent.conf"

    echo ""
    echo "Checking Homebrew env..."
    brew doctor
